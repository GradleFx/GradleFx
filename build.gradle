/*
* Copyright (c) 2011 the original author or authors
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*      http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/

import org.gradle.api.XmlProvider
import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

group = 'org.gradlefx'
version = '0.6.2'
ext.packaging = 'jar'

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

//set build variables based on build type (release, continuous integration, development)
if(hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    testCompile "org.spockframework:spock-core:0.6-groovy-1.8"
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.6'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}

task wrapper(type: Wrapper) {
    group = 'Buildmaster'
    description = 'Generates gradlew and gradlew.bat bootstrap scripts'
    gradleVersion = '1.1'
    // place jar file and properties into a subdirectory to avoid root dir clutter
    jarFile = 'buildSrc/wrapper/gradle.jar'
}

//********* artifact signing *********
if(isReleaseBuild) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        }
        else {
            mavenDeployer {
                if(isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }
    
                repository(url: sonatypeRepositoryUrl) {
                  authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
    
                pom.project {
                   name 'GradleFx'
                   packaging 'jar'
                   description 'GradleFx is a Gradle plugin for building Flex and Actionscript applications'
                   url 'http://gradlefx.github.com/'
    
                   scm {
                       url 'scm:git@github.com:GradleFx/GradleFx.git'
                       connection 'scm:git@github.com:GradleFx/GradleFx.git'
                       developerConnection 'scm:git@github.com:GradleFx/GradleFx.git'
                   }
    
                   licenses {
                       license {
                           name 'The Apache Software License, Version 2.0'
                           url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                           distribution 'repo'
                       }
                   }
    
                   developers {
                       developer {
                           id 'yennicktrevels'
                           name 'Yennick Trevels'
                       }
                       developer {
                           id 'stevendick'
                           name 'Steven Dick'
                       }
                   }
               }
            }
        }
    }
}
